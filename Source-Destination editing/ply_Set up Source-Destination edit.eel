/* 
@description Set up Source-Destination edit
@version 1.2
@author Paweł Łyżwa (ply)
@about
  Based on https://forum.cockos.com/showthread.php?t=116213
@changelog enable move envelope points with items
*/

EnumProjects(-1, #src_filename);
Main_OnCommand(41929, 0); // New project tab ignoring default template
Main_openProject(#src_filename);

Undo_BeginBlock(0);

Main_OnCommand(40310, 0); // Set ripple editing per-track
!GetToggleCommandState(40070) ? Main_OnCommand(40070, 0); // Set move envelope points with items on
Main_OnCommand(40753, 0); // Disable snap
while(DeleteProjectMarkerByIndex(0, 0)); // clear all markers & regions

// delete all items
i = 0;
loop(CountTracks(0),
	track = GetTrack(0, i);
	while (CountTrackMediaItems(track)) (
		item = GetTrackMediaItem(track, 0);
		DeleteTrackMediaItem(track, item);
	);
	i += 1;
);

// reset loop, time selection and cusor position
GetSet_LoopTimeRange(1, 0, 0, 0, 0);
GetSet_LoopTimeRange(1, 1, 0, 0, 0);
SetEditCurPos(0, 1, 1);

// put first 10 markers at the start of the project
i = 1;
loop(10,
	AddProjectMarker(0, 0, 0, 0, "", i);
	i += 1;
);

Undo_EndBlock("Set up S/D edit", -1);

UpdateArrange();
MarkProjectDirty(0);

Main_SaveProject(0, 1);
